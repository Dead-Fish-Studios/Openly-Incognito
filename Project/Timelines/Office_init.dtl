if {OTHER.Day} == 1:
	jump day1_first
else:
	jump default

label day1_first
Walking in, you’re greeted by the familiar scene of the desks, chairs, and filing cabinets.
The [b]Relations Department[/b] - your department.
Being one of the smaller departments of the company, the office wasn’t much bigger than the average meeting room.
There were only two other people with you in this department\:
- Jordan
	Jordan was a greyhound who always wore an inquisitive look.
	You met her when you initially got hired, as she was also placed in the same department you were, leading to the two of you becoming good friends.
	Being that there were so few of you, you and Jordan decided to just push your desks together in the middle and save space for other things, making the place feel a bit more comfortable.
	- Molly
		The other was Molly, a black bat whose big eyes commonly had a tired look to them.
		She was your supervisor but also a good friend. You were very appreciative to them some days more than others, as working at this company could be really dry sometimes.
- Molly
	Molly was a black bat whose big eyes commonly had a tired look to them.
	She was your supervisor but also a good friend. You were very appreciative to them some days more than others, as working at this company could be really dry sometimes.
	- Jordan
		The other was Jordan, a greyhound who always wore an inquisitive look.
		You met her when you initially got hired, as she was also placed in the same department you were, leading to the two of you becoming good friends.
		Being that there were so few of you, you and Jordan decided to just push your desks together in the middle and save space for other things, making the place feel a bit more comfortable.
do Game.tod_progress(20)
For now though, you’re alone in the office.
do Game.zoom_camera_xy(185, 200, 2)
You shut the door behind you, and walk over to your desk, sinking down into your squeaky chair and leaning back.
Jesse: Sigh…wonder where Jordan is. Did she go to Molly’s meeting too?
Being Monday, today was the day when all the department heads met together for a short meeting. Typically this was the time where you and Jordan would have a few moments together before Molly came into the office.
Lately the two of you have taken to trying to scare her when she would walk into the office, and as such, have used this time to plan out your next attack.
Jordan not being here was a bit of a surprise though. Molly would usually bring one of you with her in case she needed help bringing files back down, but she had said that this week was just a check in, and no planning was going on.
Jesse: Guess I’ll just get started.
do Game.set_level_init_label("OfficeLevel", "default")
[end_timeline]

label day3_first
do Game.set_POI_visible("OfficeLevel", "JordanDay3POI", true)
Jesse: Maybe she’s in here?
Pushing open the door, you see Jordan at her desk. She’s rummaging through her drawers, seemingly looking for something.
Jordan: Where is it, where is it...
[end_timeline]

label default
if {OTHER.Day} == 3:
	if Game.get_POI_label("OfficeLevel", "JordanDay3POI") == "start":
		if Game.get_POI_label("OutsideSecurityLevel", "SecurityDoorPOI") == "done":
			jump day3_first
Jesse: Desk sweet desk.
